buildscript {

  ext {
    springBootVersion = '1.5.10.RELEASE'
    springLoadedVersion = '1.2.6.RELEASE'
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.springframework:springloaded:${springLoadedVersion}")
  }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'
apply plugin: "eclipse"
apply plugin: "checkstyle"

bootRun.dependsOn(':frontend:build')

jar {
  baseName = rootProject.name
  manifest {
    attributes(
      "Implementation-Title": rootProject.name,
      "Implementation-Version": version,
      "Main-Class": "com.alexcibotari.nakame.Application"
    )
  }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  jcenter()
  mavenCentral()
}

dependencies {

  compile "org.springframework.boot:spring-boot-starter-data-cassandra"
  compile "org.springframework.boot:spring-boot-starter-security"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-aop"

  //DEV
  compile "org.springframework.boot:spring-boot-devtools"

  //GraphQL
  compile "com.graphql-java:graphql-java:${graphqlJavaVersion}"
  compile "com.graphql-java:graphql-java-tools:${graphqlToolVersion}"
  compile "com.graphql-java:graphql-spring-boot-starter:${graphqlSpringBootVersion}"
  compile "com.graphql-java:graphiql-spring-boot-starter:${graphqlSpringBootVersion}"

  //Security
  compile "org.springframework.security.oauth:spring-security-oauth2"

  //Database
  compile "com.datastax.cassandra:cassandra-driver-core:${cassandraDriverVersion}"

  //Commons
  compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
  compile "org.apache.httpcomponents:httpclient"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

  //Test Artifacts -->
  testCompile "org.springframework.boot:spring-boot-starter-test"
  testCompile "org.cassandraunit:cassandra-unit-spring:${cassandraUnitVersion}"
  testCompile "org.springframework.security:spring-security-test"
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

eclipse {
  classpath {
    downloadJavadoc = true
    downloadSources = true
  }
}

checkstyle {
  toolVersion "8.8"
}
