buildscript {
  ext {
    springBootVersion = '2.0.3.RELEASE'
  }
  repositories {
    jcenter()
    maven { url "https://repo.spring.io/plugins-release" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "io.spring.gradle:propdeps-plugin:0.0.9.RELEASE"
    classpath "net.researchgate:gradle-release:2.7.0"
  }
}

apply plugin: "java"
apply plugin: "org.springframework.boot"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "checkstyle"
apply plugin: "propdeps"
apply plugin: "propdeps-idea"
apply plugin: "propdeps-eclipse"
apply plugin: "io.spring.dependency-management"
apply plugin: "net.researchgate.release"

bootRun.dependsOn(":frontend:build")

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
  graphqlJavaVersion = '8.0'
  graphqlToolVersion = '5.1.0'
  graphqlSpringBootVersion = '4.2.0'
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-starter-aop"
  compile "org.springframework.boot:spring-boot-starter-data-cassandra"
  compile "org.springframework.boot:spring-boot-starter-security"
  compile "org.springframework.boot:spring-boot-starter-web"

  //DEV
  compile "org.springframework.boot:spring-boot-devtools"

  //GraphQL
  compile "com.graphql-java:graphql-java:${graphqlJavaVersion}"
  compile "com.graphql-java:graphql-java-tools:${graphqlToolVersion}"
  compile "com.graphql-java:graphql-spring-boot-starter:${graphqlSpringBootVersion}"
  compile "com.graphql-java:graphiql-spring-boot-starter:${graphqlSpringBootVersion}"

  //Security
  compile "org.springframework.security:spring-security-oauth2-core"
  compile "org.springframework.security:spring-security-oauth2-jose"
  compile "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.0.1.RELEASE"

  //Commons
  compile "org.apache.httpcomponents:httpclient"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

  optional "org.springframework.boot:spring-boot-configuration-processor"

  //Test Artifacts -->
  testCompile "org.springframework.boot:spring-boot-starter-test"
  testCompile "org.springframework.security:spring-security-test"
}

compileJava.dependsOn(processResources)

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

eclipse {
  classpath {
    downloadJavadoc = true
    downloadSources = true
  }
}

release {
  preCommitText = '[skip ci]'
  preTagCommitMessage = 'pre tag commit: '
  tagCommitMessage = 'creating tag: '
  newVersionCommitMessage = 'new version commit: '
  buildTasks = ['assemble']
  git {
    requireBranch = "master|develop"
  }
}

test {
  failFast = true
}

checkstyle {
  toolVersion "8.10"
}

springBoot {
  buildInfo()
}
